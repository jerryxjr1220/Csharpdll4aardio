//aardio传递到C#中DataTable显示大量数据的优化办法

import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=759;bottom=469)
winform.add(
custom={cls="custom";text="自定义控件";left=0;top=0;right=760;bottom=470;bgcolor=12639424;db=1;dl=1;dr=1;dt=1;z=1}
)
/*}}*/
 
import console
console.open()
 
import System.Windows.Forms;
var Forms = System.Windows.Forms;
var dataGridView = Forms.CreateEmbed("DataGridView",winform.custom); 
dataGridView.ColumnHeadersHeightSizeMode = 2; //避免在高分屏下错乱 
   
import System.Type;
import System.Data; 
  
var Data = System.Data;
var dt =Data.DataTable("DT"); 
//定义列格式
var integerColumn = Data.DataColumn("RowIndex",System.Type.GetType("System.Int32"));
var stringColumn = Data.DataColumn("StringColumn",System.Type.GetType("System.String"));
var decimalColumn = Data.DataColumn("DecimalColumn",System.Type.GetType("System.Decimal"));
var boolColumn = Data.DataColumn("BooleanColumn",System.Type.GetType("System.Boolean"));
//绑定格式
dt.Columns.Add(integerColumn);
dt.Columns.Add(stringColumn);
dt.Columns.Add(decimalColumn);
dt.Columns.Add(boolColumn);
 
//填充大量数据
var data={}
for(i=1;50000;1){
    data[i] = { i, "Txt"+i,math.random(), math.round(math.random()*10)%2?true:false };
}
 
//--------------------------------------------------------
//转换aardio的table表到c#的List表
var listdata = dotNet.createArrayList(data);
 
//引用数据传递dll
var convertdll = dotNet.load("\ListTodataTable.dll");
var Table = convertdll.import("ListTodataTable.Table");//Create
//写入list数据到DataTable
Table.WriteTo(dt,listdata);
//------------------------------------------------------
 
//显示数据
var dataView = System.Data.DataView(dt);
dataGridView.DataSource = dataView;
dataGridView.EditMode=2; 
   
 
winform.show();
win.loopMessage();
